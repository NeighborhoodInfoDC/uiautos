<!doctype html public "-//w3c//dtd html 4.0 final//en">
<html>
   <head>
      <title>K:\Metro\PTatian\UISUG\Uiautos\ListNoDup.sas</title>
   </head>
   <body>
      <pre>
/******************* URBAN INSTITUTE MACRO LIBRARY *********************
 
 Macro: ListNoDup

 Description: Autocall macro returns list with duplicate entries 
 removed.
 
 Use: Function
 
 Author: Peter Tatian
 
***********************************************************************/

%macro ListNoDup(
  list,           /* List of items */
  delim=%str( )   /* Delimiter for list (def. blank char) */
  );

  /*************************** USAGE NOTES *****************************
   
   SAMPLE CALL: 
     %ListNoDup( A.B.C.D.E.B.F.A.C.G, delim=. )
       returns unduplicated list A.B.C.D.E.F.G

  *********************************************************************/

  /*************************** UPDATE NOTES ****************************
  
  02-01-16 PAT Corrected error when list has more than 2 duplicates of
               same item.

  *********************************************************************/

  %***** ***** ***** MACRO SET UP ***** ***** *****;
   
  %local ListNoDup scanlist target i v;
    
    
  %***** ***** ***** ERROR CHECKS ***** ***** *****;

  %if &amp;delim = { or &amp;delim = } %then %do;
    %err_mput( macro=ListNoDup, msg=Curly braces { } cannot be used as list delimiters. )
    %goto exit;
  %end;

  %if %index( &amp;list, {{bol}} ) &gt; 0 or %index( &amp;list, {{eol}} ) &gt; 0 %then %do;
    %err_mput( macro=ListNoDup, msg=The text "{{bol}}" or "{{eol}}" must not appear in the list. )
    %goto exit;
  %end;

  %***** ***** ***** MACRO BODY ***** ***** *****;

  %let ListNoDup = ;
  %let scanlist = {{bol}}&amp;delim&amp;list&amp;delim{{eol}};
  %let target = %scan( &amp;scanlist, 2, &amp;delim );

  %do %while ( %length( &amp;target ) &gt; 0 and &amp;target ~= {{eol}} );
  
    %** Add item to unduplicated list **;
    %if %length( &amp;ListNoDup ) = 0 %then
      %let ListNoDup = &amp;target;
    %else
      %let ListNoDup = &amp;ListNoDup&amp;delim&amp;target;
      
    %** Remove all other occurances of item **;

    %let i = 1;
    %let v = %scan( &amp;scanlist, &amp;i, &amp;delim );
    %let newscanlist = ;

    %do %until ( &amp;v = );

      %if &amp;v ~= &amp;target %then %do;
        %if %length( &amp;newscanlist ) = 0 %then
          %let newscanlist = &amp;v;
        %else 
          %let newscanlist = &amp;newscanlist&amp;delim&amp;v;
      %end;

      %let i = %eval( &amp;i + 1 );
      %let v = %scan( &amp;scanlist, &amp;i, &amp;delim );

    %end;

    %let scanlist = &amp;newscanlist;
    %let target = %scan( &amp;scanlist, 2, &amp;delim );

  %end;

  %let ListNoDup = %unquote( &amp;ListNoDup );
  &amp;ListNoDup

  %exit:


  %***** ***** ***** CLEAN UP ***** ***** *****;

%mend ListNoDup;


/************************ UNCOMMENT TO TEST ***************************

*options mprint symbolgen mlogic;

filename uiautos "K:\Metro\PTatian\UISUG\Uiautos";
options sasautos=(uiautos sasautos);

%** This test generates an error **;
%let list = A{B{C{D{E{B{F{A{C{G;
%let undup = [%ListNoDup( &amp;list, delim={ )];
%put _user_;

%** This test generates an error **;
%let list = A B {{bol}} C D;
%let undup = [%ListNoDup( &amp;list )];
%put _user_;

%let list = A.B.C.D.E.B.F.A.C.G;
%let undup = [%ListNoDup( &amp;list, delim=. )];
%put _user_;

%let list = .A.B.C.D.E.B.F.A.C.G.;
%let undup = [%ListNoDup( &amp;list, delim=. )];
%put _user_;

%let list = ..A...B.C..D.E.B.F.A.C.G;
%let undup = [%ListNoDup( &amp;list, delim=. )];
%put _user_;

%let list = A B C D E B F A C G;
%let undup = [%ListNoDup( &amp;list )];
%put _user_;

%let list = %str(   A   B   C   D E B    F A  C   G   );
%let undup = [%ListNoDup( &amp;list )];
%put _user_;

%let list = A B C AA AAA D E B AA F A AA C G AAAA;
%let undup = [%ListNoDup( &amp;list )];
%put _user_;

%let list = 0001 0001 0002 0002 0002 0002 0002 0002 0002 0100;
%let undup = [%ListNoDup( &amp;list )];
%put _user_;

/**********************************************************************/

      </pre>
   </body>
</html>
