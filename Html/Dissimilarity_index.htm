<!doctype html public "-//w3c//dtd html 4.0 final//en">
<html>
   <head>
      <title>K:\Metro\PTatian\UISUG\Uiautos/Dissimilarity_index.sas</title>
   </head>
   <body>
      <pre>
/******************* URBAN INSTITUTE MACRO LIBRARY *********************
 
 Macro: Dissimilarity_index

 Description: Autocall macro to calculate the dissimilarity index between
 two populations.
 
 Use: Open code
 
 Author: Peter Tatian
 
***********************************************************************/

/***** Macro %Dissimilarity_index() - Calculates dissimilarity index *****/

%macro Dissimilarity_index( 
  data=,        /** Input data set **/
  out=,         /** Output data set &amp; var name (optional) **/
  varA=,        /** Population A **/
  varB=,        /** Population B **/
  by=,          /** By variable for grouping results (optional) **/
  print=y       /** Print results (Y/N) **/
  );

  /*************************** USAGE NOTES *****************************
   
   SAMPLE CALL: 
     %Dissimilarity_index( data=Test, varA=A, varB=B, by=geo, out=Result )
       calculates dissimilarity index for populations A and B for units
       defined by var GEO, results saved to Result data set

  *********************************************************************/

  /*************************** UPDATE NOTES ****************************

   07/01/11  Peter A. Tatian

  *********************************************************************/

  %***** ***** ***** MACRO SET UP ***** ***** *****;
   
  %local input_data varDI;

  %if &amp;out = %then %do;
    %let out = _di_output;
    %let varDI = _DI;
  %end;
  %else %do;
    %let varDI = %DSNameOnly( &amp;out );
  %end;
    
    
  %***** ***** ***** ERROR CHECKS ***** ***** *****;



  %***** ***** ***** MACRO BODY ***** ***** *****;

  ** Calculate total group A and B populations **;

  %if &amp;by ~= %then %do;
    %let input_data = _di_sorted;
    proc sort data=&amp;data out=&amp;input_data;
      by &amp;by;
    run;
  %end;
  %else %do;
    %let input_data = &amp;data;
  %end;
    
  proc means data=&amp;input_data noprint nway;
    %if &amp;by ~= %then %do;
      class &amp;by;
    %end;
    var &amp;varA &amp;varB;
    output out=_di_totals sum=&amp;varA._s &amp;varB._s;
  run;  
  
  /***
  proc print data=_di_totals;
    id &amp;by;
    title3 "_di_totals";
  run;
  ***/

  ** Merge population totals to individual unit data **;
  
  data _di_calc;

    %if &amp;by ~= %then %do;
      merge &amp;input_data (keep=&amp;by &amp;varA &amp;varB) _di_totals;
        by &amp;by;
    %end;
    %else %do;
    
      set &amp;input_data (keep=&amp;varA &amp;varB);
      
      if _n_ = 1 then set _di_totals;
    
    %end;
    
    ** Calculate dissimilarity index formula for each unit **;

    if &amp;varA._s &gt; 0 and &amp;varB._s &gt; 0 then 
      &amp;varDI = 0.5 * abs( ( &amp;varA / &amp;varA._s ) - ( &amp;varB / &amp;varB._s ) );
    
    label &amp;varDI = "Dissimilarity index of &amp;varA vs. &amp;varB";
    
  run;

  /***
  proc print data=_di_calc (obs=15);
    var &amp;by &amp;varA &amp;varB &amp;varA._s &amp;varB._s &amp;varDI;
    title3 "_di_calc";
  run;
  ***/

  ** Sum DI for all units **;

  proc means data=_di_calc noprint nway;
    %if &amp;by ~= %then %do;
      class &amp;by;
    %end;
    var &amp;varDI;
    output out=&amp;out (drop=_type_ _freq_) sum= ;
  run;

  %if %mparam_is_yes( &amp;print ) %then %do;
  
    ** Print DI results **;

    proc print data=&amp;out label noobs;
      %if &amp;by ~= %then %do;
        id &amp;by;
      %end;
      var &amp;varDI;
    run;
    
  %end;
  
  
  %***** ***** ***** CLEAN UP ***** ***** *****;

  ** Cleanup temporary data sets **;

  proc datasets library=work memtype=(data) nolist nowarn;
    delete _di_:;
  quit;

%mend Dissimilarity_index;



/************************ UNCOMMENT TO TEST ***************************

** Locations of SAS autocall macro libraries **;

filename uiautos  "K:\Metro\PTatian\UISUG\Uiautos";
options sasautos=(uiautos sasautos);

options nocenter;
options mprint nosymbolgen nomlogic;

data Test;

  input id geo A B;
  
datalines;
1 1 10 20
2 1 50 0
3 1 40 40
4 1 0 20
5 2 80 40
6 2 20 90
7 2 60 50
8 2 30 0
9 2 0 0
10 2 40 60
11 3 0 50
12 3 0 70
;

run;

proc print data=Test;
  title2 'Input data set: TEST';
run;

title2;

%Dissimilarity_index( data=Test, varA=A, varB=B, by=geo, out= )
%Dissimilarity_index( data=Test, varA=A, varB=B, by=, out=work.di_out, print=n )

%File_info( data=di_out )

/**********************************************************************/
      </pre>
   </body>
</html>
