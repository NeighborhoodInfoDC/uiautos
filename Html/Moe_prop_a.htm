<!doctype html public "-//w3c//dtd html 4.0 final//en">
<html>
   <head>
      <title>K:\Metro\PTatian\UISUG\Uiautos/Moe_prop_a.sas</title>
   </head>
   <body>
      <pre>
/******************* URBAN INSTITUTE MACRO LIBRARY *********************
 
 Macro: Moe_prop_a

 Description: Autocall macro that returns a calculation for a margin of error 
 for a derived proportion based on the values and margins of error of the 
 proportion numerator and denominator.

 The numerator of a proportion must be a subset of the denominator 
 (e.g., the proportion of single person households that are female).
 
 Alternate version of macro that assigns value to a specified variable
 (var=), rather than just returning the MOE value as %Moe_prop() does. 
 This version tests whether quantity in sqrt() is negative and, if so,
 uses the %Moe_ratio() value instead, as suggested in the Census doc.

 Assigned value can be scaled by optional multiplier (mult=1).
 
 Method based on 
 http://www.census.gov/acs/www/Downloads/handbooks/ACSResearch.pdf
 pp. A-14 - A-15.
 
 Use: Within data step
 
 Author: Peter Tatian
 
***********************************************************************/

%macro Moe_prop_a( 
  var=,       /** Variable to which MOE value is assigned **/
  mult=1,     /** Optional multiplier for MOE value **/
  num=,       /** Proportion numerator value **/
  den=,       /** Proportion denominator value **/
  num_moe=,   /** Numerator margin of error **/
  den_moe=    /** Denominator margin of error **/
  );

  /*************************** USAGE NOTES *****************************
   
   SAMPLE CALL: 
     %moe_prop_a( var=female_prop_moe, num=female, den=total, num_moe=female_moe, den_moe=total_moe )
       Assigns margin of error for proportion female/total to var
       female_prop_moe

  *********************************************************************/

  /*************************** UPDATE NOTES ****************************

  11/19/12  Peter A. Tatian

  *********************************************************************/

  %***** ***** ***** MACRO SET UP ***** ***** *****;
   
  %local prop;
    
    
  %***** ***** ***** ERROR CHECKS ***** ***** *****;



  %***** ***** ***** MACRO BODY ***** ***** *****;
  
  %let prop = ((&amp;num)/(&amp;den));
  
  %if &amp;mult = %then %let mult = 1;
  
  if ( ((&amp;num_moe)*(&amp;num_moe)) - ((&amp;prop*&amp;prop) * ((&amp;den_moe)*(&amp;den_moe))) ) &gt;= 0 then do;
    &amp;var = (&amp;mult) * %moe_prop( num=&amp;num, den=&amp;den, num_moe=&amp;num_moe, den_moe=&amp;den_moe );
  end;
  else do; 
    &amp;var = (&amp;mult) * %moe_ratio( num=&amp;num, den=&amp;den, num_moe=&amp;num_moe, den_moe=&amp;den_moe );
  end;

%mend Moe_prop_a;


/************************ UNCOMMENT TO TEST ***************************

options mprint;

data _null_;

  prop = 45/100;
  %moe_prop_a( var=moe1, num=45, den=100, num_moe=10, den_moe=15 );
  put prop= moe1=;

  %moe_prop_a( var=moe2, num=45, den=100, num_moe=10, den_moe=0, mult=100 );
  put prop= moe2=;

run;

/**********************************************************************/

      </pre>
   </body>
</html>
